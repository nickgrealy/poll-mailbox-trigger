
plugins {
//    id "co.riiid.gradle" version "0.4.2"
    id 'org.jenkins-ci.jpi' version '0.18.1'
}

defaultTasks 'clean', 'jpi'

/**
=== Usage ===
gradlew jpi - Build the Jenkins plugin file, which can then be found in the build directory. The file will currently end in ".hpi".
gradlew publishToMavenLocal - Build the Jenkins plugin and install it into your local Maven repository.
gradlew publish - Deploy your plugin to the Jenkins Maven repository to be included in the Update Center.
gradlew server - Start a local instance of Jenkins (http://localhost:8080) with the plugin pre-installed for testing and debugging.
 */

ext.enforceJavaVersion = 1.6

apply from: "$rootDir/config/gradle/common.gradle"
apply from: "$rootDir/config/gradle/staticCodeAnalysis.gradle"


repositories {
    mavenLocal()
    maven { url 'http://bits.netbeans.org/maven2/' }
    maven { url 'http://repo.jenkins-ci.org/releases/' }
    mavenCentral()
    jcenter()
}

jenkinsPlugin {
    coreVersion = '1.581'
    shortName = project.name
    displayName = jenkinsDisplayName
    url = "https://wiki.jenkins-ci.org/display/JENKINS/$jenkinsDisplayName"
    gitHubUrl = "https://github.com/jenkinsci/$project.name"
    compatibleSinceVersion = '1.424'
    fileExtension = 'hpi'
    developers {
        developer {
            id 'nickg'
            name 'Nick Grealy'
            email 'nickgrealy@gmail.com'
        }
    }
}

dependencies {
    compile "org.jenkins-ci.lib:xtrigger-lib:0.25"
    compile "com.sun.mail:javax.mail:1.5.1"

    testCompile('org.hamcrest:hamcrest-core:1.3') { force = true }
    testCompile('junit:junit:4.12') { force = true }
    testCompile 'org.jvnet.mock-javamail:mock-javamail:1.9'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile "info.cukes:cucumber-junit:1.2.4"
    testCompile "info.cukes:cucumber-java:1.2.4"
}

/**
 * We must exclude the junit-dep dependency, because it is clashing with junit when we run the AcceptanceTests.
 */
configurations.findAll {!it.name.endsWith('junit-dep')}.each { conf ->
    conf.exclude group: "junit", module: "junit-dep"
}

jpi.outputs.upToDateWhen { false }
jpi.doLast {
    verifySize(file(jpi.archivePath), 638)
}

task unzip(type: Copy) {
    from zipTree(file("$buildDir/libs/poll-mailbox-trigger-plugin.hpi"))
    into file("${buildDir}/unpacked/dist")
}

unzip.dependsOn jpi

findbugs { sourceSets = [sourceSets.main] }

configurations.runtime {
    resolutionStrategy {
        force 'commons-codec:commons-codec:1.8'
        force 'org.jenkins-ci:trilead-ssh2:build217-jenkins-5'
        force 'commons-io:commons-io:2.4'
        force 'org.codehaus.groovy:groovy-all:1.8.9'
        force 'org.kohsuke.stapler:stapler:1.228'
        force 'org.jenkins-ci:annotation-indexer:1.7'
        force 'commons-beanutils:commons-beanutils:1.8.3'
        force 'commons-lang:commons-lang:2.6'
        force 'commons-logging:commons-logging:1.1.3'
        force 'commons-collections:commons-collections:3.2.1'
        force 'jaxen:jaxen:1.1-beta-11'
        force 'net.java.dev.jna:jna:3.4.0'
        force 'org.jvnet.localizer:localizer:1.10'
        force 'org.springframework:spring-core:2.5.6.SEC03'
        force 'com.google.guava:guava:14.0'
        force 'com.jcraft:jzlib:1.1.3'
        force 'org.springframework:spring-beans:2.5.6.SEC03'
        force 'org.springframework:spring-context:2.5.6.SEC03'
    }
}